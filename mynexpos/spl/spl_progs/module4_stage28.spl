
alias functionNum R1;
alias currentPID R2;

//Terminal Write
if( functionNum== 3 ) then
	
	print R3;
	
	return;
endif;	


//Terminal Read
if( functionNum == 4 ) then

	backup;
	//Acquire Terminal
	R1=8;
	R2=[SYSTEM_STATUS_TABLE + 5*CORE  +1];
	call MOD_0;
	restore;

	read;
	[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 5*CORE  +1]*16 + 4]=WAIT_TERMINAL;
	[KERN_LOCK]=0;
	backup;
	call MOD_5;
	restore;
	backup;
	R1=1;
	call MOD_8;
	restore;

	[[PTBR + (2*(R3/512))]*512 + (R3%512)] = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 5*CORE  +1]*16 + 8];
	return;
endif;	



//Disk Load
if( functionNum == 2 ) then 


	backup;
	//Acquire Disk
	
	R1=3;
	R2=[SYSTEM_STATUS_TABLE + 5*CORE  +1];
	call MOD_0;
	restore;

	//Status
	[DISK_STATUS_TABLE]=1;
	
	//PID
	[DISK_STATUS_TABLE+4] = currentPID;
	
	//Load or Store
	[DISK_STATUS_TABLE +1]=0;
	
	//Page Number is Argument R3
	[DISK_STATUS_TABLE+2] = R3;
	
	//Block Number is Argument R4
        [DISK_STATUS_TABLE+3] = R4;
        
	
	load(R3,R4);
	
	[PROCESS_TABLE + currentPID*16 + 4] = WAIT_DISK;
	[KERN_LOCK]=0;

	backup;
	call MOD_5;


	restore;
	backup;
	R1=1;
	call MOD_8;
	restore;
	return;
endif;


//Disk Store
if( functionNum == 1 ) then

	backup;
	//Acquire Disk
	R1=3;
	call MOD_0;

	restore;

	//Status
	[DISK_STATUS_TABLE]=1;
	
	//PID
	[DISK_STATUS_TABLE+4] = currentPID;
	
	//Load or Store
	[DISK_STATUS_TABLE +1]=1;
	
	//Page Number is Argument R3
	[DISK_STATUS_TABLE+2] = R3;
	
	//Block Number is Argument R4
        [DISK_STATUS_TABLE+3] = R4;
        

        [PROCESS_TABLE + currentPID*16 + 4] = WAIT_DISK;
        store(R3,R4);
	[KERN_LOCK]=0;
	backup;
	call MOD_5;
	restore;
	backup;
	R1=1;
	call MOD_8;
	restore;
	return;
endif;
















